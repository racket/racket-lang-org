
    Title:200!
    Date:2013-03-25T14:00:00.000-04:00
    Tags:

*posted by Asumu Takikawa*

About a month ago, inspired by a mailing list post by Tim Brown, Racketeers started to write more solutions to [Rosetta Code](http://rosettacode.org/wiki/Rosetta_Code) tasks for Racket. Just today, we've reached 200 entries in the [Racket category](http://rosettacode.org/wiki/Category:Racket)!

This is a nice milestone, but we still have a ways to go. At 200 entries, Racket comes in at around 54th in the [popularity](http://rosettacode.org/wiki/RC_POP.OUT) rankings. So if you're looking to practice your Racketeering skills, don't hesitate to work on some of the [remaining tasks](http://rosettacode.org/wiki/Reports:Tasks_not_implemented_in_Racket).

To give you a taste of the kinds of solutions we have so far, here are some examples.

Mandelbrot:

```racket
(define (iterations a z i)
  (define z′ (+ (* z z) a))
  (if (or (= i 255) (> (magnitude z′) 2))
      i
      (iterations a z′ (add1 i))))
 
(define (iter->color i)
  (if (= i 255)
      (make-object color% "black")
      (make-object color% 
        (* 5 (modulo i 15)) (* 32 (modulo i 7)) 
          (* 8 (modulo i 31)))))
 
(define (mandelbrot width height)
  (define target (make-screen-bitmap width height))
  (define dc (new bitmap-dc% [bitmap target]))
  (for* ([x width] [y height])
    (define real-x (- (* 3.0 (/ x width)) 2.25))
    (define real-y (- (* 2.5 (/ y height)) 1.25))
    (send dc set-pen 
      (iter->color 
        (iterations 
          (make-rectangular real-x real-y) 0 0)) 1 'solid)
    (send dc draw-point x y))
  target)

> (mandelbrot300200)
```



Yin and Yang:

```racket
(define (yin-yang d)
  (define base
    (hc-append (inset/clip (circle d) 0 0 (- (/ d 2)) 0)
               (inset/clip (disk d) (- (/ d 2)) 0 0 0)))
  (define with-top
    (ct-superimpose
     base
     (cc-superimpose (colorize (disk (/ d 2)) "white")
                     (disk (/ d 8)))))
  (define with-bottom
    (cb-superimpose
     with-top
     (cc-superimpose (disk (/ d 2))
                     (colorize (disk (/ d 8)) "white"))))
  (cc-superimpose with-bottom (circle d)))
 

> (yin-yang 200)
```


Animate a pendulum:

```racket
#lang racket
 
(require 2htdp/image
         2htdp/universe)
 
(define (pendulum)
  (define (accel θ) (- (sin θ)))
  (define θ (/ pi 2.5))
  (define θ′ 0)
  (define θ′′ (accel (/ pi 2.5)))
  (define (x θ) (+ 200 (* 150 (sin θ))))
  (define (y θ) (* 150 (cos θ)))
  (λ (n)
    (define p-image 
      (underlay/xy 
        (add-line 
          (empty-scene 400 200) 200 0 (x θ) (y θ) "black")
            (- (x θ) 5) (- (y θ) 5) 
              (circle 5 "solid" "blue")))
    (set! θ (+ θ (* θ′ 0.04)))
    (set! θ′ (+ θ′ (* (accel θ) 0.04)))
    p-image))
 
(animate (pendulum))


Jensen's Device:

```racket
#lang algol60
begin
   integer i;
   real procedure sum (i, lo, hi, term);
      value lo, hi;
      integer i, lo, hi;
      real term;
      comment term is passed by-name, and so is i;
   begin
      real temp;
      temp := 0;
      for i := lo step 1 until hi do
         temp := temp + term;
      sum := temp
   end;
   comment note the correspondence between 
   the mathematical notation and the call to sum;
   printnln (sum (i, 1, 100, 1/i))
end
```

Thanks to all of the people who have contributed solutions so far!

<!-- more -->



* * *

Racket is climbing the rankings ...

http://timb.net/popular-languages.html

— *Daniel Prager, 7 June 2013*

* * *

